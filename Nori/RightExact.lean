import Mathlib.CategoryTheory.Limits.Preserves.Basic
import Nori.Mathlib.CategoryTheory.Limits.Shapes.Kernels
import Nori.LiftAbelian

universe u v u' v'

open CategoryTheory Category Functor Limits

open scoped ZeroObject

variable {A : Type u'} [Category.{v'} A] [Abelian A]

--variable (C : Type u) [Category.{v} C] [Preadditive C] [HasZeroObject C] (F : C ‚•§ A) [F.Additive]

namespace CategoryTheory

namespace Adel

namespace RightExact

open CandidateCoker

variable {X Y : ComposableArrows A 2} (u : X ‚ü∂ Y)

instance : IsIso (ShortComplex.cyclesMap ((contractLeft A).map (candœÄ u))) where
  out := by
    set h := ((contractLeft A).obj (candcoker u)).leftHomologyData
    set h' := ((contractLeft A).obj Y).leftHomologyData
    use KernelFork.mapOfIsLimit (KernelFork.ofŒπ h.i h.wi) h'.hi (Arrow.homMk biprod.fst biprod.fst
      (by change biprod.fst ‚â´ Y.map' 1 2 = biprod.map (Y.map' 1 2) (ùüô (X.obj two)) ‚â´ biprod.fst
          simp))
    refine ‚ü®?_, ?_‚ü©
    ¬∑ ext
      rw [id_comp, assoc]
      erw [KernelFork.mapOfIsLimit_Œπ]
      simp only [Fork.ofŒπ_pt, const_obj_obj, parallelPair_obj_zero, Arrow.mk_left, Fork.Œπ_ofŒπ,
        Arrow.homMk_left]
      change ShortComplex.cyclesMap' _ h' h ‚â´ _ = _
      rw [ShortComplex.cyclesMap'_i_assoc]
      change h'.i ‚â´ biprod.inl ‚â´ _ = _
      erw [biprod.inl_fst]
      rw [comp_id]
      rfl
    ¬∑ ext
      rw [id_comp, assoc, ShortComplex.cyclesMap_i]
      erw [KernelFork.mapOfIsLimit_Œπ_assoc]
      simp only [Fork.ofŒπ_pt, parallelPair_obj_zero, Fork.Œπ_ofŒπ, Arrow.homMk_left]
      change _ ‚â´ _ ‚â´ biprod.inl = _
      ext
      ¬∑ simp only [assoc, BinaryBicone.inl_fst, comp_id]
        rfl
      ¬∑ simp only [assoc, BinaryBicone.inl_snd, comp_zero]
        have eq : (biprod.snd : ((contractLeft A).obj (candcoker u)).X‚ÇÇ ‚ü∂ X.obj two) =
            ((contractLeft A).obj (candcoker u)).g ‚â´ biprod.snd := by
          change _ = biprod.map (Y.map' 1 2) (ùüô (X.obj two)) ‚â´ biprod.snd
          rw [biprod.map_snd, comp_id]
        erw [eq]
        simp

lemma cyclesMap_inv_iCycle : inv (ShortComplex.cyclesMap ((contractLeft A).map (candœÄ u))) ‚â´
    ((contractLeft A).obj Y).iCycles = ((contractLeft A).obj (candcoker u)).iCycles ‚â´ biprod.fst := by
  rw [‚Üê cancel_epi (ShortComplex.cyclesMap ((contractLeft A).map (candœÄ u))), IsIso.hom_inv_id_assoc,
    ShortComplex.cyclesMap_i_assoc]
  change _ = _ ‚â´ biprod.inl ‚â´ _
  erw [biprod.inl_fst]
  rw [comp_id]

@[reassoc]
lemma image_snd : kernel.Œπ ((candcoker u).map' 0 1 ‚â´ (candcoker u).map' 1 2) ‚â´
    biprod.snd ‚â´ X.map' 1 2 = 0 := by
  have : kernel.Œπ ((candcoker u).map' 0 1 ‚â´ (candcoker u).map' 1 2) ‚â´ ((candcoker u).map' 0 1 ‚â´
      (candcoker u).map' 1 2) ‚â´ biprod.snd = 0 := by
    rw [‚Üê assoc _ _ biprod.snd]
    convert zero_comp
    exact kernel.condition _
  change _ ‚â´ ((biprod.map (Y.map' 0 1) (X.map' 1 2) + biprod.snd ‚â´ u.app one ‚â´ biprod.inl) ‚â´
    biprod.map (Y.map' 1 2) (ùüô (X.obj two))) ‚â´ biprod.snd = 0 at this
  simp only [Preadditive.add_comp, assoc, biprod.inl_map, biprod.map_snd, comp_id,
    BinaryBicone.inl_snd, comp_zero, add_zero] at this
  exact this

@[reassoc]
lemma image_fst : kernel.Œπ ((candcoker u).map' 0 1 ‚â´ (candcoker u).map' 1 2) ‚â´
    biprod.fst ‚â´ Y.map' 0 1 ‚â´ Y.map' 1 2 = 0 := by
  have : kernel.Œπ ((candcoker u).map' 0 1 ‚â´ (candcoker u).map' 1 2) ‚â´ ((candcoker u).map' 0 1 ‚â´
      (candcoker u).map' 1 2) ‚â´ biprod.fst = 0 := by
    rw [‚Üê assoc _ _ biprod.fst]
    convert zero_comp
    exact kernel.condition _
  change _ ‚â´ ((biprod.map (Y.map' 0 1) (X.map' 1 2) + biprod.snd ‚â´ u.app one ‚â´ biprod.inl) ‚â´
    biprod.map (Y.map' 1 2) (ùüô (X.obj two))) ‚â´ biprod.fst = 0 at this
  simp only [Preadditive.add_comp, assoc, biprod.inl_map, biprod.map_fst, BinaryBicone.inl_fst,
    comp_id, Preadditive.comp_add] at this
  rw [biprod.map_fst_assoc, ‚Üê u.naturality, image_snd_assoc, zero_comp, add_zero] at this
  exact this

lemma liftCycles_homology : ((contractLeft A).obj Y).liftCycles (kernel.Œπ _ ‚â´ biprod.fst ‚â´ Y.map' 0 1)
    (by simp only [assoc]; exact image_fst u) ‚â´ ((contractLeft A).obj Y).homologyœÄ = 0 := by
  have eq : ((contractLeft A).obj Y).liftCycles (kernel.Œπ _ ‚â´ biprod.fst ‚â´ Y.map' 0 1)
      (by simp only [assoc]; exact image_fst u) = kernel.lift (Y.map' 0 1 ‚â´ Y.map' 1 2)
      (kernel.Œπ _ ‚â´ biprod.fst) (by simp only [assoc]; exact image_fst _) ‚â´
      ((contractLeft A).obj Y).toCycles := by
    rw [‚Üê cancel_mono ((contractLeft A).obj Y).iCycles]
    simp only [ShortComplex.liftCycles_i, assoc, ShortComplex.toCycles_i]
    change _ = _ ‚â´ (kernel.Œπ _ ‚â´ _)
    rw [kernel.lift_Œπ_assoc, assoc]
  rw [eq]
  simp

lemma toCycles_cyclesMap_inv_eq : ((contractLeft A).obj (candcoker u)).toCycles ‚â´
    inv (ShortComplex.cyclesMap ((contractLeft A).map (candœÄ u))) =
    ((contractLeft A).obj Y).liftCycles (kernel.Œπ _ ‚â´ biprod.fst ‚â´ Y.map' 0 1)
    (by simp only [assoc]; exact image_fst u)
    + ((contractLeft A).obj Y).liftCycles (kernel.Œπ _ ‚â´ biprod.snd ‚â´ u.app one)
    (by simp only [assoc]; erw [‚Üê u.naturality]; rw [image_snd_assoc, zero_comp]) := by
  rw [‚Üê cancel_mono ((contractLeft A).obj Y).iCycles, assoc]
  rw [cyclesMap_inv_iCycle, ‚Üê assoc, ShortComplex.toCycles_i]
  conv_lhs => dsimp [contractLeft]
  simp only [Preadditive.comp_add, Preadditive.add_comp, assoc]
  rw [biprod.map_fst, biprod.inl_fst, comp_id]
  simp only [ShortComplex.liftCycles_i]
  rfl

instance : Epi ((homologyLeft A).map (candœÄ u)) := by
  rw [epi_iff_surjective_up_to_refinements]
  intro A‚ÇÄ a‚ÇÄ
  obtain ‚ü®A‚ÇÅ, œÄ, _, a‚ÇÅ, h‚ÇÅ‚ü© := (epi_iff_surjective_up_to_refinements ((contractLeft A).obj
    (candcoker u)).homologyœÄ).mp inferInstance a‚ÇÄ
  use A‚ÇÅ, œÄ, inferInstance
  use a‚ÇÅ ‚â´ inv (ShortComplex.cyclesMap ((contractLeft A).map (candœÄ u))) ‚â´
    ((contractLeft A).obj Y).homologyœÄ
  rw [h‚ÇÅ]
  simp only [assoc]
  congr 1
  rw [‚Üê cancel_epi (ShortComplex.cyclesMap ((contractLeft A).map (candœÄ u))),
    IsIso.hom_inv_id_assoc]
  exact (ShortComplex.homologyœÄ_naturality _).symm

lemma homology_comp_zero : (homologyLeft A).map u ‚â´ (homologyLeft A).map (candœÄ u) = 0 := by
  rw [‚Üê Functor.map_comp, ‚Üê (homologyLeft A).map_zero]
  exact homologyLeft_map_eq_of_homotopic _ _ _ _ (candcondition u)

lemma exact : (ShortComplex.mk _ _ (homology_comp_zero u)).Exact := by
  rw [ShortComplex.exact_iff_exact_up_to_refinements]
  intro A‚ÇÄ a‚ÇÄ h‚ÇÄ
  dsimp at a‚ÇÄ h‚ÇÄ
  obtain ‚ü®A‚ÇÅ, œÄ, _, a‚ÇÅ, h‚ÇÅ‚ü© := (epi_iff_surjective_up_to_refinements
    ((contractLeft A).obj Y).homologyœÄ).mp inferInstance a‚ÇÄ
  set S := ShortComplex.mk ((contractLeft A).obj (candcoker u)).toCycles ((contractLeft A).obj
    (candcoker u)).homologyœÄ ((contractLeft A).obj (candcoker u)).toCycles_comp_homologyœÄ
  obtain ‚ü®A‚ÇÇ, œÄ', _, a‚ÇÇ, h‚ÇÇ‚ü© := S.exact_iff_exact_up_to_refinements.mp
    (S.exact_of_g_is_cokernel ((contractLeft A).obj (candcoker u)).homologyIsCokernel)
    (a‚ÇÅ ‚â´ ShortComplex.cyclesMap ((contractLeft A).map (candœÄ u)))
    (by rw [assoc, ‚Üê ShortComplex.homologyœÄ_naturality, ‚Üê assoc a‚ÇÅ, ‚Üê h‚ÇÅ, assoc]
        convert comp_zero)
  have h‚ÇÇ' : (a‚ÇÇ ‚â´ kernel.Œπ _ ‚â´ biprod.snd) ‚â´ ((contractLeft A).obj X).g = 0 := by
    change _ ‚â´ X.map' 1 2 = 0
    simp only [assoc]
    rw [image_snd, comp_zero]
  use A‚ÇÇ, œÄ' ‚â´ œÄ, inferInstance, ((contractLeft A).obj X).liftCycles _ h‚ÇÇ' ‚â´
    ((contractLeft A).obj X).homologyœÄ
  simp only [homologyLeft, comp_obj, ShortComplex.homologyFunctor_obj, Functor.comp_map,
    ShortComplex.homologyFunctor_map, assoc, ShortComplex.homologyœÄ_naturality]
  apply_fun (fun x ‚Ü¶ x ‚â´ inv (ShortComplex.cyclesMap ((contractLeft A).map (candœÄ u)))) at h‚ÇÇ
  rw [assoc, assoc, IsIso.hom_inv_id, comp_id] at h‚ÇÇ
  rw [h‚ÇÅ, ‚Üê assoc, h‚ÇÇ, assoc a‚ÇÇ, toCycles_cyclesMap_inv_eq, Preadditive.comp_add,
    Preadditive.add_comp, assoc, liftCycles_homology, comp_zero, zero_add, ‚Üê assoc _ (ShortComplex.cyclesMap _)]
  congr 1
  rw [‚Üê cancel_mono ((contractLeft A).obj Y).iCycles]
  simp only [assoc, ShortComplex.liftCycles_i, ShortComplex.liftCycles_comp_cyclesMap]
  rfl

section Preserves

noncomputable def preservesCokernels_aux : IsColimit ((homologyLeftAbelian A).mapCocone ((Cocones.precompose
    (compNatIso' (quotient A)).inv).obj (cocone_aux u))) := by
  have := (ShortComplex.exact_and_epi_g_iff_g_is_cokernel _).mp ‚ü®RightExact.exact u, inferInstance‚ü©
  dsimp at this
  set Œ± : parallelPair ((quotient A).map u) 0 ‚ãô homologyLeftAbelian A ‚âÖ
      parallelPair ((homologyLeft A).map u) 0 := by
    refine parallelPair.ext ((quotient_homologyLeftAbelian A).app X) ((quotient_homologyLeftAbelian A).app Y) ?_ ?_
    ¬∑ dsimp; rw [‚Üê (quotient_homologyLeftAbelian A).hom.naturality]; rfl
    ¬∑ dsimp; simp
  refine (IsColimit.equivOfNatIsoOfIso Œ± _ _ ?_).invFun (Classical.choice this)
  refine Cocones.ext (Iso.refl _) (fun j ‚Ü¶ ?_)
  match j with
  | WalkingParallelPair.zero =>
    dsimp [Œ±, compNatIso', cocone_aux]
    rw [‚Üê cancel_epi ((quotient_homologyLeftAbelian A).hom.app X)]
    simp only [comp_obj, id_comp, comp_id, Iso.hom_inv_id_app_assoc]
    conv_rhs => rw [‚Üê Functor.map_comp, ‚Üê (quotient_homologyLeftAbelian A).hom.naturality]
    convert (comp_id _).symm
  | WalkingParallelPair.one =>
    dsimp [Œ±, compNatIso', cocone_aux]
    rw [‚Üê cancel_epi ((quotient_homologyLeftAbelian A).hom.app Y)]
    simp only [comp_obj, id_comp, comp_id, Iso.hom_inv_id_app_assoc]
    rw [‚Üê (quotient_homologyLeftAbelian A).hom.naturality]
    convert (comp_id _).symm

def preservesCokernelsComposableArrows : PreservesColimit (parallelPair ((quotient A).map u) 0)
    (homologyLeftAbelian A) where
  preserves hc := by
    set e := hc.uniqueUpToIso ((IsColimit.precomposeHomEquiv (compNatIso' (quotient A)).symm
      (cocone_aux u)).invFun (cocone_isColimit u))
    have h : IsColimit ((homologyLeftAbelian A).mapCocone ((Cocones.precompose (compNatIso'
      (quotient A)).inv).obj (cocone_aux u))) := preservesCokernels_aux u
    exact Nonempty.intro (h.ofIsoColimit ((Cocones.functoriality _ (homologyLeftAbelian A)).mapIso e).symm)

instance {X Y : Adel A} (u : X ‚ü∂ Y) : PreservesColimit (parallelPair u 0) (homologyLeftAbelian A) :=
  preservesCokernels_of_preservesCokernelsComposableArrows (homologyLeftAbelian A)
  preservesCokernelsComposableArrows u

end Preserves

end RightExact

/-
The morally correct way to do would be to use opposite categories.
-/
namespace LeftExact

open CandidateKer

variable {X Y : ComposableArrows A 2} (u : X ‚ü∂ Y)

instance : IsIso (ShortComplex.opcyclesMap ((contractRight A).map (candŒπ u))) where
  out := by
    set h := ((contractRight A).obj (candker u)).rightHomologyData
    set h' := ((contractRight A).obj X).rightHomologyData
    use CokernelCofork.mapOfIsColimit h'.hp (CokernelCofork.ofœÄ h.p h.wp)
      (Arrow.homMk biprod.inl biprod.inl
      (by change biprod.inl ‚â´ biprod.map (X.map' 0 1) (ùüô (Y.obj zero)) = X.map' 0 1 ‚â´ biprod.inl
          simp))
    refine ‚ü®?_, ?_‚ü©
    ¬∑ ext
      rw [comp_id, ShortComplex.p_opcyclesMap_assoc]
      erw [CokernelCofork.œÄ_mapOfIsColimit h'.hp (CokernelCofork.ofœÄ h.p h.wp)]
      simp only [ Arrow.mk_right, Cofork.ofœÄ_pt, const_obj_obj, Arrow.mk_left,
        Arrow.homMk_right, Cofork.œÄ_ofœÄ]
      change biprod.fst ‚â´ _ = h.p
      ext
      ¬∑ rw [biprod.inl_fst_assoc]
        rfl
      ¬∑ rw [biprod.inr_fst_assoc, zero_comp]
        have eq : biprod.inr = biprod.inr ‚â´ ((contractRight A).obj (candker u)).f := by
          change _ = _ ‚â´ biprod.map _ _
          rw [biprod.inr_map, id_comp]
        rw [eq]
        simp
    ¬∑ ext
      rw [comp_id]
      erw [CokernelCofork.œÄ_mapOfIsColimit_assoc h'.hp (CokernelCofork.ofœÄ h.p h.wp)]
      simp only [Arrow.mk_right, Arrow.mk_left, Arrow.homMk_right, Cofork.ofœÄ_pt, Cofork.œÄ_ofœÄ]
      change _ ‚â´ ((contractRight A).obj (candker u)).pOpcycles ‚â´ _ = _
      rw [ShortComplex.p_opcyclesMap]
      change _ ‚â´ biprod.fst ‚â´ _ = _
      conv_lhs => erw [biprod.inl_fst_assoc]

@[reassoc]
lemma pOpcycles_opcyclesMap_inv :
    ((contractRight A).obj X).pOpcycles ‚â´ inv (ShortComplex.opcyclesMap ((contractRight A).map
    (candŒπ u))) = biprod.inl ‚â´ ((contractRight A).obj (candker u)).pOpcycles := by
  rw [‚Üê cancel_mono (ShortComplex.opcyclesMap ((contractRight A).map (candŒπ u))), assoc,
    IsIso.inv_hom_id, comp_id, assoc, ShortComplex.p_opcyclesMap]
  change _ = _ ‚â´ biprod.fst ‚â´ _
  erw [biprod.inl_fst_assoc]

@[reassoc]
lemma inr_image : Y.map' 0 1 ‚â´ biprod.inr ‚â´ cokernel.œÄ
    ((candker u).map' 0 1 ‚â´ (candker u).map' 1 2) = 0 := by
  have : biprod.inr ‚â´ ((candker u).map' 0 1 ‚â´ (candker u).map' 1 2) ‚â´
      cokernel.œÄ ((candker u).map' 0 1 ‚â´ (candker u).map' 1 2) = 0 := by
    rw [cokernel.condition, comp_zero]
  change biprod.inr ‚â´ (biprod.map (X.map' 0 1) (ùüô (Y.obj zero)) ‚â´
    (biprod.map (X.map' 1 2) (Y.map' 0 1) + biprod.fst ‚â´ u.app one ‚â´ biprod.inr)) ‚â´ _ = 0 at this
  simp only [Preadditive.comp_add, biprod.map_fst_assoc, NatTrans.naturality_assoc,
    Preadditive.add_comp, assoc, biprod.inr_map_assoc, id_comp, BinaryBicone.inr_fst_assoc,
    zero_comp, add_zero] at this
  exact this

@[reassoc]
lemma inl_image : X.map' 0 1 ‚â´ X.map' 1 2 ‚â´ biprod.inl ‚â´
    cokernel.œÄ ((candker u).map' 0 1 ‚â´ (candker u).map' 1 2) = 0 := by
  have : biprod.inl ‚â´ ((candker u).map' 0 1 ‚â´ (candker u).map' 1 2) ‚â´
      cokernel.œÄ ((candker u).map' 0 1 ‚â´ (candker u).map' 1 2) = 0 := by
    rw [cokernel.condition, comp_zero]
  change biprod.inl ‚â´ (biprod.map (X.map' 0 1) (ùüô (Y.obj zero)) ‚â´
    (biprod.map (X.map' 1 2) (Y.map' 0 1) + biprod.fst ‚â´ u.app one ‚â´ biprod.inr)) ‚â´ _ = 0 at this
  simp only [Preadditive.comp_add, biprod.map_fst_assoc, NatTrans.naturality_assoc,
    Preadditive.add_comp, assoc, biprod.inl_map_assoc, BinaryBicone.inl_fst_assoc] at this
  rw [inr_image, comp_zero, add_zero] at this
  exact this

lemma homology_descOpcycles : ((contractRight A).obj X).homologyŒπ ‚â´
    ((contractRight A).obj X).descOpcycles (X.map' 1 2 ‚â´ biprod.inl ‚â´ cokernel.œÄ _)
    (inl_image u) = 0 := by
  have eq : ((contractRight A).obj X).descOpcycles (X.map' 1 2 ‚â´ biprod.inl ‚â´ cokernel.œÄ _)
    (inl_image u) = ((contractRight A).obj X).fromOpcycles ‚â´ cokernel.desc (X.map' 0 1 ‚â´
    X.map' 1 2) (biprod.inl ‚â´ cokernel.œÄ _) (by simp only [assoc]; exact inl_image u) := by
    rw [‚Üê cancel_epi ((contractRight A).obj X).pOpcycles]
    simp only [ShortComplex.p_descOpcycles, ShortComplex.p_fromOpcycles_assoc]
    change _ = (_ ‚â´ cokernel.œÄ (X.map' 0 1 ‚â´ X.map' 1 2)) ‚â´ _
    rw [assoc, cokernel.œÄ_desc]
  rw [eq]
  simp

lemma toCycles_cyclesMap_inv_eq : inv (ShortComplex.opcyclesMap ((contractRight A).map (candŒπ u)))
    ‚â´ ((contractRight A).obj (candker u)).fromOpcycles =
    ((contractRight A).obj X).descOpcycles (X.map' 1 2 ‚â´ biprod.inl ‚â´ cokernel.œÄ _)
    (inl_image u)
    + ((contractRight A).obj X).descOpcycles (u.app one ‚â´ biprod.inr ‚â´ cokernel.œÄ _)
    (by erw [u.naturality_assoc]; rw [inr_image, comp_zero]) := by
  rw [‚Üê cancel_epi ((contractRight A).obj X).pOpcycles]
  rw [pOpcycles_opcyclesMap_inv_assoc, ShortComplex.p_fromOpcycles]
  conv_lhs => dsimp [contractRight]
  simp only [Preadditive.comp_add, Preadditive.add_comp, assoc]
  rw [biprod.inl_map_assoc, biprod.inl_fst_assoc]
  simp only [ShortComplex.p_descOpcycles]
  rfl

instance : Mono ((homologyRight A).map (candŒπ u)) := by
  rw [Preadditive.mono_iff_cancel_zero]
  intro A‚ÇÄ a‚ÇÄ h‚ÇÄ
  dsimp [homologyRight] at h‚ÇÄ
  refine (Preadditive.mono_iff_cancel_zero ((contractRight A).obj (candker u)).homologyŒπ).mp
    inferInstance A‚ÇÄ a‚ÇÄ ?_
  rw [‚Üê cancel_mono (ShortComplex.opcyclesMap ((contractRight A).map (candŒπ u))), zero_comp]
  rw [assoc, ‚Üê ShortComplex.homologyŒπ_naturality, ‚Üê assoc, h‚ÇÄ, zero_comp]

lemma homology_comp_zero : (homologyRight A).map (candŒπ u) ‚â´ (homologyRight A).map u = 0 := by
  rw [‚Üê Functor.map_comp, ‚Üê (homologyRight A).map_zero]
  exact homologyRight_map_eq_of_homotopic _ _ _ _ (candcondition u)

lemma exact : (ShortComplex.mk _ _ (homology_comp_zero u)).Exact := by
  rw [ShortComplex.exact_iff_exact_up_to_refinements]
  intro A‚ÇÄ a‚ÇÄ h‚ÇÄ
  dsimp at a‚ÇÄ h‚ÇÄ
  set a‚ÇÅ := a‚ÇÄ ‚â´ ((contractRight A).obj X).homologyŒπ with ha‚ÇÅ
  have : a‚ÇÅ ‚â´ inv (ShortComplex.opcyclesMap ((contractRight A).map (candŒπ u))) ‚â´
      ((contractRight A).obj (candker u)).fromOpcycles = 0 := by
    rw [toCycles_cyclesMap_inv_eq, Preadditive.comp_add]
    conv_lhs => congr; rw [ha‚ÇÅ, assoc, homology_descOpcycles, comp_zero]
    rw [zero_add]
    obtain ‚ü®A‚ÇÅ, œÄ, _, a‚ÇÅ', h‚ÇÅ'‚ü© := (epi_iff_surjective_up_to_refinements
      ((contractRight A).obj X).pOpcycles).mp inferInstance a‚ÇÅ
    rw [‚Üê cancel_epi œÄ, ‚Üê assoc œÄ a‚ÇÅ, h‚ÇÅ', assoc, ShortComplex.p_descOpcycles, comp_zero]
    have h‚ÇÅ'' : (a‚ÇÅ' ‚â´ u.app one) ‚â´ ((contractRight A).obj Y).pOpcycles = 0 := by
      change (a‚ÇÅ' ‚â´ ((contractRight A).map u).œÑ‚ÇÇ) ‚â´ _ = 0
      rw [assoc, ‚Üê ShortComplex.p_opcyclesMap, ‚Üê assoc, ‚Üê h‚ÇÅ', ha‚ÇÅ, assoc, assoc,
        ‚Üê ShortComplex.homologyŒπ_naturality, ‚Üê assoc a‚ÇÄ]
      erw [h‚ÇÄ]
      simp
    set S := ShortComplex.mk (Y.map' 0 1) ((contractRight A).obj Y).pOpcycles
      ((contractRight A).obj Y).f_pOpcycles
    obtain ‚ü®A‚ÇÇ, œÄ', _, a‚ÇÇ, h‚ÇÇ‚ü© := S.exact_iff_exact_up_to_refinements.mp (S.exact_of_g_is_cokernel
      ((contractRight A).obj Y).opcyclesIsCokernel) (a‚ÇÅ' ‚â´ u.app one) h‚ÇÅ''
    rw [‚Üê cancel_epi œÄ', reassoc_of% h‚ÇÇ, inr_image, comp_zero, comp_zero]
  set a‚ÇÇ := ((contractRight A).obj (candker u)).liftHomology (a‚ÇÅ ‚â´ inv (ShortComplex.opcyclesMap
    ((contractRight A).map (candŒπ u)))) (by rw [assoc, this])
  use A‚ÇÄ, ùüô _, inferInstance, a‚ÇÇ
  change _ = _ ‚â´ (homologyRight A).map (candŒπ u)
  rw [id_comp, ‚Üê cancel_mono ((contractRight A).obj X).homologyŒπ, assoc]
  dsimp [homologyRight]
  rw [ShortComplex.homologyŒπ_naturality, ‚Üê ha‚ÇÅ]
  change _ = a‚ÇÇ ‚â´ ((contractRight A).obj (candker u)).homologyŒπ ‚â´ _
  rw [‚Üê assoc, ShortComplex.liftHomology_Œπ, assoc, IsIso.inv_hom_id, comp_id]

section Preserves

noncomputable def preservesKernels_aux : IsLimit ((homologyRightAbelian A).mapCone ((Cones.postcompose
    (compNatIso' (quotient A)).hom).obj (cone_aux u))) := by
  have := (ShortComplex.exact_and_mono_f_iff_f_is_kernel _).mp ‚ü®LeftExact.exact u, inferInstance‚ü©
  dsimp at this
  set Œ± : parallelPair ((quotient A).map u) 0 ‚ãô homologyRightAbelian A ‚âÖ
      parallelPair ((homologyRight A).map u) 0 := by
    refine parallelPair.ext ((quotient_homologyRightAbelian A).app X)
      ((quotient_homologyRightAbelian A).app Y) ?_ ?_
    ¬∑ dsimp; rw [‚Üê (quotient_homologyRightAbelian A).hom.naturality]; rfl
    ¬∑ dsimp; simp
  refine (IsLimit.equivOfNatIsoOfIso Œ± _ _ ?_).invFun (Classical.choice this)
  refine Cones.ext (Iso.refl _) (fun j ‚Ü¶ ?_)
  match j with
  | WalkingParallelPair.zero =>
    dsimp [Œ±, compNatIso', cone_aux]
    rw [‚Üê cancel_mono ((quotient_homologyRightAbelian A).inv.app X)]
    simp only [comp_obj, comp_id, assoc,
      Iso.hom_inv_id_app, id_comp, NatTrans.naturality, Functor.comp_map]
    conv_rhs => erw [‚Üê (quotient_homologyRightAbelian A).inv.naturality]
    convert (comp_id _).symm
  | WalkingParallelPair.one =>
    dsimp [Œ±, compNatIso', cone_aux]
    rw [‚Üê cancel_mono ((quotient_homologyRightAbelian A).inv.app Y)]
    simp only [comp_obj, comp_id, map_comp, assoc,
      Iso.hom_inv_id_app, id_comp, NatTrans.naturality, Functor.comp_map, NatTrans.naturality_assoc]
    rw [‚Üê Functor.map_comp]; erw [‚Üê (quotient_homologyRightAbelian A).inv.naturality]
    convert (comp_id _).symm

lemma preservesKernelsComposableArrows :
    PreservesLimit (parallelPair ((quotient A).map u) 0) (homologyRightAbelian A) where
  preserves hc := by
    set e := hc.uniqueUpToIso ((IsLimit.postcomposeHomEquiv (compNatIso' (quotient A))
      (cone_aux u)).invFun (cone_isLimit u))
    have h : IsLimit ((homologyRightAbelian A).mapCone ((Cones.postcompose (compNatIso'
      (quotient A)).hom).obj (cone_aux u))) := preservesKernels_aux u
    exact Nonempty.intro (h.ofIsoLimit ((Cones.functoriality _ (homologyRightAbelian A)).mapIso e).symm)

instance {X Y : Adel A} (u : X ‚ü∂ Y) : PreservesLimit (parallelPair u 0) (homologyRightAbelian A) :=
  preservesKernels_of_preservesKernelsComposableArrows (homologyRightAbelian A)
  preservesKernelsComposableArrows u

instance {X Y : Adel A} (u : X ‚ü∂ Y) : PreservesLimit (parallelPair u 0) (homologyLeftAbelian A) :=
  preservesLimit_of_natIso (parallelPair u 0) (homologyIsoAbelian A).symm

end Preserves

end LeftExact

end Adel

end CategoryTheory
